plugins {
    id 'com.github.hierynomus.license' version '0.11.0'
}

group 'com.github.jonathanxd'
version '1.3-pre'


allprojects {
    apply plugin: "idea"
    apply plugin: 'java'
    apply plugin: 'license'
    apply plugin: 'maven'
    apply plugin: 'signing'

    license {
        header rootProject.file("LICENSE_HEADER")
    }
}
//Sonatype upload
apply from: 'upload.gradle'

defaultTasks 'licenseFormat', 'build', 'test', 'jar'

sourceCompatibility = 1.8

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
        }        
        resources {
            srcDirs 'src/main/resources'
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

// Tasks
jar {
    from "LICENSE"

    manifest {
        attributes 'Implementation-Title': 'TextLexer',
                'Implementation-Version': version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

subprojects {
    dependencies {
        compile rootProject
        testCompile rootProject.sourceSets.test.output
    }
}

project(':TextLexerShow') {
    dependencies {
        compile group: 'org.tinyjee.jgraphx', name: 'jgraphx', version: '3.4.1.3'

    }
}

allprojects{
    dependencies {
        testCompile group: 'junit', name:'junit', version: '4.11'
    }
}

allprojects{
    repositories {
        mavenCentral()
    }
}

allprojects {
    test {
        useJUnit()
    }
}


license {
    ext.name = 'TextLexer'
    ext.organization = 'TheRealBuggy/JonathanxD (https://github.com/JonathanxD/)'
    ext.description = 'Lexical Analyzer API for Java!'
    ext.url = 'https://github.com/JonathanxD/TextLexer'
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.email = 'jonathan.scripter@programmer.net'

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"
    
    header rootProject.file('LICENSE_HEADER')
    
    sourceSets = project.sourceSets
    
    ignoreFailures false
    strictCheck true
    
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
